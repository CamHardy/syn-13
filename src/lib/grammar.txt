program -> declaration* EOF ;

declaration -> variableDeclaration
						 | functionDeclaration
						 | statement ;

variableDeclaration -> 'var' IDENTIFIER ( '=' expression )? ';' ;
functionDeclaration -> 'fun' function ;
function -> IDENTIFIER '(' parameters? ')' block ;
parameters -> IDENTIFIER ( ',' IDENTIFIER )* ;
statement -> expressionStatement
					 | forStatement
					 | ifStatement
					 | printStatement 
					 | returnStatement
					 | whileStatement
					 | block ;

expressionStatement -> expression ';' ;
forStatement -> 'for' '(' ( variableDeclaration | expressionStatement | ';' ) expression? ';' expression? ')' statement ;
ifStatement -> 'if' '(' expression ')' statement ( 'else' statement )? ;
printStatement -> 'print' expression ';' ;
returnStatement -> 'return' expression? ';' ;
whileStatement -> 'while' '(' expression ')' statement ;
block -> '{' declaration* '}' ;

expression -> assignment ;

assignment -> IDENTIFIER '=' assignment 
						| logic_or ;

logic_or -> logic_and ( 'or' logic_and )* ;
logic_and -> equality ( 'and' equality )* ;

equality -> comparison ( ( '==' | '!=' ) comparison )* ;
comparison -> term ( ( '>' | '>=' | '<' | '<=' ) term )* ;
term -> factor ( ( '-' | '+' ) factor )* ;
factor -> unary ( ( '/' | '*' ) unary )* ;

unary -> ( '!' | '-' ) unary ;
call -> primary ( '(' arguments? ')' )* ;
primary -> 'true' | 'false' | 'nil' | NUMBER | STRING | IDENTIFIER | '(' expression ')' ;
arguments -> expression ( ',' expression )* ;

NUMBER -> DIGIT+ ( '.' DIGIT+ )? ;
STRING -> '"' <any char except '"'>* '"' ;
IDENTIFIER -> ALPHA ( ALPHA | DIGIT )* ;
ALPHA -> 'a' ... 'z' | 'A' ... 'Z' | '_' ;
DIGIT -> '0' ... '9' ;